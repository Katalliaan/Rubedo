import groovy.json.JsonSlurper

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

// Load mcmod.info into config
for(srcDir in sourceSets.main.resources.srcDirs) {
	def configFile = file "${srcDir}/mcmod.info"
	
	configFile.withReader {
		def json = new JsonSlurper().parse it
		project.ext.config = new ConfigObject()
		project.ext.config.putAll json[0]
		project.version = config.version
	}
}

group= config.namespace
archivesBaseName = config.modid + "-" + config.mcversion.tokenize('-')[1]

minecraft {
    version = config.mcversion
    runDir = "eclipse"
    
    // replacing
    replace "@NAME@", config.name
    replace "@VERSION@", config.version
    replaceIn "rubedo/RubedoIntegration.java"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
        
    from(sourceSets.main.resources.srcDirs)
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourceJar
    archives deobfJar
}
